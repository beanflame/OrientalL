+引用 宇宙
+实例 宇宙 宇宙
+导入 洛书·标准流
+实例 标准流 标准流
+导入 洛书·随机数
+实例 随机数 随机数
+导入 洛书·文件
+实例 文件 文件
+实例 文件 文件2
+类 宇宙控制台
{
    +方法 启动宇宙(进化,进攻,密度)
    {
        宇宙·宇宙大爆炸(进化+"10",进化,进攻+"10",进攻)
        +循环("10")
        {
            宇宙·创建文明()
            标准流·等待("200")
        }
        +循环("-1")
        {
            文件·打开文件("文明库/.time","0")
            文件·写入(<宇宙·时间>)
            文件·关闭()
            宇宙·创建文明()
            +循环(密度)
            {
                随机数·生成("1","100")
                +如果(<随机数·生成>)>("5")
                {
                    +如果(<随机数·生成>)<=("10")
                    {
                        宇宙·黑暗森林打击()
                    }
                }
                +如果(<随机数·生成>)<=("5")
                {
                    宇宙·文明战争()
                }
                +如果(<随机数·生成>)>=("70")
                {
                    宇宙·文明接触()
                }
                +如果(<随机数·生成>)>("10")
                {
                    +如果(<随机数·生成>)<("70")
                    {
                        宇宙·文明进化()
                    }
                }
                标准流·等待("200")
            }
            随机数·生成("100","500")
            标准流·等待(<随机数·生成>)
        }
        +回收 进化
        +回收 进攻
        +回收 密度
    }
    +方法 宇宙状态()
    {
        文件·打开文件("文明库/.time","-1")
        文件·读取()
        时间 = <文件·读取>
        文件·关闭()
        文件·打开文件("文明库/.num","-1")
        文件·读取()
        数量 = <文件·读取>
        文件·关闭()
        标准流·输出("宇宙状态\n文明数   "&数量&"\n时间  "&时间&"(千年)\n")
    }
    +方法 文明属性(文明)
    {
        文件·打开文件("文明库/"&文明&".wm","-1")
        文件·读取()
        文明指数 = <文件·读取>
        文件·关闭()
        文件·打开文件("文明库/"&文明&".jg","-1")
        文件·读取()
        进攻 = <文件·读取>
        文件·关闭()
        文件·打开文件("文明库/"&文明&".jh","-1")
        文件·读取()
        进化 = <文件·读取>
        文件·关闭()
        标准流·输出("文明编号   "&文明&"\n文明指数  "&文明指数&"\n进化指数  "&进化&"\n进攻指数  "&进攻&"\n")
        +回收 文明
        +回收 文明指数
        +回收 进化
        +回收 进攻
    }
    +方法 文明历程(文明)
    {
        标准流·运行("cp 文明库/"&文明&".lc 文明历程.txt")
        标准流·输出("已保存到 文明历程.txt\n")
        +回收 文明
    }
}
+导入 洛书·标准流
+导入 洛书·文件
+导入 洛书·随机数
+实例 文件 文件
+实例 随机数 随机数
+实例 标准流 标准流
+类 宇宙
{
    +属性 时间
    +属性 文明数
    +属性 进攻下限
    +属性 进攻上限
    +属性 进化上限
    +属性 进化下限
    +方法 宇宙大爆炸(进化上限,进化下限,进攻上限,进攻下限)
    {
        <[此类]·时间> = "0"
        +线程 宇宙时间
        {
            //启动一个子线程进行计时
            +循环 ("-1")
            {
                <[此类]·时间> = <[此类]·时间> + "1"
                标准流·等待("150")
            }
        }
        <[此类]·文明数> = "0"
        <[此类]·进攻上限> = 进攻上限
        <[此类]·进攻下限> = 进攻下限
        <[此类]·进化上限> = 进化上限
        <[此类]·进化下限> = 进化下限
        文件·打开文件("文明库/.num","0")
        文件·写入("0")
        文件·关闭()
        +回收 文明密度
        +回收 进攻上限
        +回收 进攻下限
        +回收 进化上限
        +回收 进化下限
    }
    +方法 创建文明()
    {
        //文明属性 编号 文明指数 进化指数 进攻指数 文明历程
        <[此类]·文明数> = <[此类]·文明数> + "1"
        文件·打开文件("文明库/"&<[此类]·文明数>&".wm","0")
        文件·写入("0")
        文件·关闭()
        随机数·生成(<[此类]·进化下限>,<[此类]·进化上限>)
        文件·打开文件("文明库/"&<[此类]·文明数>&".jh","0")
        文件·写入(<随机数·生成>)
        文件·关闭()
        随机数·生成(<[此类]·进攻下限>,<[此类]·进攻上限>)
        文件·打开文件("文明库/"&<[此类]·文明数>&".jg","0")
        文件·写入(<随机数·生成>)
        文件·关闭()
        文件·打开文件("文明库/"&<[此类]·文明数>&".lc","0")
        文件·关闭()

        文件·打开文件("文明库/"&<[此类]·文明数>&".lc","1")
        文件·写入("宇宙纪年>>"&<[此类]·时间>&"(千年)    生成\n")
        文件·关闭()

        文件·打开文件("文明库/.num","0")
        文件·写入(<[此类]·文明数>)
        文件·关闭()
        
    }
    +方法 文明进化()
    {
        //获取要进化的文明编号
        随机数·生成("1",<[此类]·文明数>)
        文明 = <随机数·生成>

        //获取原始文明指数
        文件·打开文件("文明库/"&文明&".wm","-1")
        文件·读取()
        文明指数 = <文件·读取>
        文件·关闭()

        //获取进化值
        文件·打开文件("文明库/"&文明&".jh","-1")
        文件·读取()
        进化值 = <文件·读取>
        文件·关闭()
        
        //计算指数
        文明指数 = 文明指数 + 进化值

        //更新文明指数
        文件·打开文件("文明库/"&文明&".wm","0")
        文件·写入(文明指数)
        文件·关闭()

        //写入文明历程
        文件·打开文件("文明库/"&文明&".lc","1")
        文件·写入("宇宙纪年>>"&<[此类]·时间>&"(千年)    进化至 "&文明指数&"\n")
        文件·关闭()
        
        +回收 文明
        +回收 文明指数
        +回收 进化值
    }
    +方法 文明接触()
    {
        接触时间 = <[此类]·时间>
        //获取要接触的文明编号
        随机数·生成("1",<[此类]·文明数> - "1")
        文明1 = <随机数·生成>
        标准流·等待("100")
        随机数·生成("1",<[此类]·文明数> - "1")
        文明2 = <随机数·生成>
        +如果 (文明1) == (文明2)
        {
            文明2 = "1" + 文明2
        }
        //获取文明指数
        文件·打开文件("文明库/"&文明1&".wm","-1")
        文件·读取()
        文明指数 = <文件·读取>
        文件·关闭()
        //获取进化值
        文件·打开文件("文明库/"&文明1&".jh","-1")
        文件·读取()
        进化值 = <文件·读取>
        文件·关闭()
        //获取倒退值
        文件·打开文件("文明库/"&文明2&".jg","-1")
        文件·读取()
        倒退值 = <文件·读取>
        文件·关闭()
        //计算结果
        文明指数 = 文明指数 - 倒退值 + 进化值
        //刷新结果
        文件·打开文件("文明库/"&文明1&".wm","0")
        文件·写入(文明指数)
        文件·关闭()
        //写入历程
        文件·打开文件("文明库/"&文明1&".lc","1")
        文件·写入("宇宙纪年>>"&接触时间&"(千年)    与第"&文明2&"号文明发生接触\n")
        文件·关闭()
        文件·打开文件("文明库/"&文明2&".lc","1")
        文件·写入("宇宙纪年>>"&接触时间&"(千年)    与第"&文明1&"号文明发生接触\n")
        文件·关闭()

        +回收 接触时间
        +回收 文明指数
        +回收 文明1
        +回收 文明2
        +回收 进化值
        +回收 倒退值
    }
    +方法 文明战争()
    {
        接触时间 = <[此类]·时间>
        //获取要接触的文明编号
        随机数·生成("1",<[此类]·文明数> - "1")
        文明1 = <随机数·生成>
        标准流·等待("100")
        随机数·生成("1",<[此类]·文明数> - "1")
        文明2 = <随机数·生成>
        +如果 (文明1) == (文明2)
        {
            文明2 = "1" + 文明2
        }
        //获取文明指数
        文件·打开文件("文明库/"&文明1&".wm","-1")
        文件·读取()
        文明指数1 = <文件·读取>
        文件·关闭()
        文件·打开文件("文明库/"&文明2&".wm","-1")
        文件·读取()
        文明指数2 = <文件·读取>
        文件·关闭()
        //计算结果
        +如果(文明指数1)<(文明指数2)
        {
            //文明1灭亡
            文件·打开文件("文明库/"&文明1&".wm","0")
            文件·写入("0")
            文件·关闭()
            文件·打开文件("文明库/"&文明1&".lc","1")
            文件·写入("宇宙纪年>>"&接触时间&"(千年)    在与第"&文明2&"号文明的战争中被毁灭了\n")
            文件·关闭()
            文件·打开文件("文明库/"&文明2&".lc","1")
            文件·写入("宇宙纪年>>"&接触时间&"(千年)    在战争中毁灭了第"&文明1&"号文明\n")
            文件·关闭()
        }
        +如果(文明指数1)>(文明指数2)
        {
            //文明1灭亡
            文件·打开文件("文明库/"&文明2&".wm","0")
            文件·写入("0")
            文件·关闭()
            文件·打开文件("文明库/"&文明2&".lc","1")
            文件·写入("宇宙纪年>>"&接触时间&"(千年)    在与第"&文明1&"号文明的战争中被毁灭了\n")
            文件·关闭()
            文件·打开文件("文明库/"&文明1&".lc","1")
            文件·写入("宇宙纪年>>"&接触时间&"(千年)    在战争中毁灭了第"&文明2&"号文明\n")
            文件·关闭()
        }
        +回收 接触时间
        +回收 文明指数1
        +回收 文明指数2
        +回收 文明1
        +回收 文明2
    }
    +方法 黑暗森林打击()
    {
        打击时间 = <[此类]·时间>
        //抽取幸运文明
        随机数·生成("1",<[此类]·文明数>)
        文明 = <随机数·生成>

        文件·打开文件("文明库/"&文明&".wm","0")
        文件·写入("0")
        文件·关闭()
        文件·打开文件("文明库/"&文明&".lc","1")
        文件·写入("宇宙纪年>>"&打击时间&"(千年)    因黑暗森林打击而毁灭\n")
        文件·关闭()

        +回收 打击时间
        +回收 文明
    }
}